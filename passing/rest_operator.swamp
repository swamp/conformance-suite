/*
 * Copyright (c) Peter Bjorklund. All rights reserved. https://github.com/swamp/conformance-suite
 * Licensed under the MIT License. See LICENSE in the project root for license information.
 */

struct GameEffect {
    name: String<32>,
    damage: Int,
    duration: Float,
    is_magical: Bool,
}

impl GameEffect {
    fn default() -> GameEffect {
        GameEffect {
            name: "Basic Attack",
            damage: 10,
            duration: 1.0,
            is_magical: false,
        }
    }
}

struct PlayerStats {
    name: String<42>,
    health: Int,
    mana: Int,
    is_alive: Bool,
    speed: Float,
}

struct Position {
    x: Int,
    y: Int,
}

struct Weapon {
    name: String<42>,
    damage: Int,
    position: Position,
    is_enchanted: Bool,
}


#[test]
fn rest_operator_with_effect_default() {
    fireball := GameEffect { name: "Fireball", damage: 50.. }

    assert(fireball.name == "Fireball", 'spell name should be overwritten <{fireball.name}>')
    assert(fireball.damage == 50, "damage should be overwritten")
    assert(fireball.duration == 1.0, "duration should use struct default")
    assert(fireball.is_magical == false, "is_magical should use struct default")
}

#[test]
fn rest_operator_with_effect_default_minimal() {
    spell := GameEffect { name: "Lightning Bolt".. }

    assert(spell.name == "Lightning Bolt", "spell name should be overwritten")

    assert(spell.damage == 10, "damage should use struct default")
    assert(spell.duration == 1.0, "duration should use struct default")
    assert(spell.is_magical == false, "is_magical should use struct default")
}

#[test]
fn rest_operator_without_player_default() {
    player := PlayerStats { name: "Aragorn", is_alive: true.. }

    assert(player.name == "Aragorn", "player name should be provided value")
    assert(player.is_alive == true, "is_alive should be provided value")

    assert(player.health == 0, "health should use Int default (0)")
    assert(player.mana == 0, "mana should use Int default (0)")
    assert(player.speed == 0.0, "speed should use Float default (0.0)")
}

#[test]
fn rest_operator_without_player_default_minimal() {
    player := PlayerStats { name: "Legolas".. }

    assert(player.name == "Legolas", "name should be provided")
    assert(player.health == 0, "health should use Int default")
    assert(player.mana == 0, "mana should use Int default")
    assert(player.is_alive == false, "is_alive should use Bool default")
    assert(player.speed == 0.0, "speed should use Float default")
}

#[test]
fn rest_operator_mixed_weapon_defaults() {
    sword := Weapon { name: "Excalibur".. }

    assert(sword.name == "Excalibur", "weapon name should be provided")
    assert(sword.damage == 0, "damage should use Int default")
    assert(!sword.is_enchanted, "is_enchanted should use Bool default")

    assert(sword.position.x == 0, "position.x should be zero-initialized")
    assert(sword.position.y == 0, "position.y should be zero-initialized")
}

#[test]
fn rest_operator_all_effect_fields_provided() {
    spell := GameEffect {
        name: "Meteor",
        damage: 100,
        duration: 3.5,
        is_magical: true..
    }

    assert(spell.name == "Meteor", "name should be provided")
    assert(spell.damage == 100, "damage should be provided")
    assert(spell.duration == 3.5, "duration should be provided")
    assert(spell.is_magical, "is_magical should be provided")
}

#[test]
fn rest_operator_empty_effect_with_default() {
    basic_spell := GameEffect { .. }

    assert(basic_spell.name == "Basic Attack", "name should use struct default")
    assert(basic_spell.damage == 10, "damage should use struct default")
    assert(basic_spell.duration == 1.0, "duration should use struct default")
    assert(!basic_spell.is_magical, "is_magical should use struct default")
}

#[test]
fn rest_operator_empty_player_without_default() {
    print("Testing rest operator with no player fields provided (pure field defaults)")

    newbie := PlayerStats { .. }

    assert(newbie.name == "", "name should use String default (empty)")
    assert(newbie.health == 0, "health should use Int default (0)")
    assert(newbie.mana == 0, "mana should use Int default (0)")
    assert(!newbie.is_alive, "is_alive should use Bool default (false)")
    assert(newbie.speed == 0.0, "speed should use Float default (0.0)")
}

struct Character {
    primary_spell: GameEffect,
    stats: PlayerStats,
    is_boss: Bool,
}

#[test]
fn rest_operator_nested_character() {
    boss := Character {
        primary_spell: GameEffect { name: "Dark Magic", damage: 75.. },
        is_boss: true..
    }

    assert(boss.primary_spell.name == "Dark Magic", "boss spell name")
    assert(boss.primary_spell.damage == 75, "boss spell damage")
    assert(boss.primary_spell.duration == 1.0, "boss spell should use struct default duration")
    assert(boss.primary_spell.is_magical == false, "boss spell should use struct default magical")

    assert(boss.stats.name == "", "stats should be zero-initialized")
    assert(boss.stats.health == 0, "stats should be zero-initialized")
    assert(boss.stats.mana == 0, "stats should be zero-initialized")

    assert(boss.is_boss, "is_boss should be provided value")
}
